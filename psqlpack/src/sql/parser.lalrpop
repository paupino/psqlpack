use sql::ast::*;
use sql::lexer::{self};

grammar<'input>;

extern {
    enum lexer::Token {
        "(" => lexer::Token::LeftBracket,
        ")" => lexer::Token::RightBracket,
        "[" => lexer::Token::LeftSquare,
        "]" => lexer::Token::RightSquare,

        "," => lexer::Token::Comma,
        ";" => lexer::Token::Semicolon,
        "=" => lexer::Token::Equals,
        "." => lexer::Token::Period,

        ACTION => lexer::Token::ACTION,
        AS => lexer::Token::AS,
        BIGINT => lexer::Token::BIGINT,
        BIGSERIAL => lexer::Token::BIGSERIAL,
        BIT => lexer::Token::BIT,
        BOOL => lexer::Token::BOOL,
        BOOLEAN => lexer::Token::BOOLEAN,
        C => lexer::Token::C,
        CASCADE => lexer::Token::CASCADE,
        CONSTRAINT => lexer::Token::CONSTRAINT,
        CHAR => lexer::Token::CHAR,
        CHARACTER => lexer::Token::CHARACTER,
        CREATE => lexer::Token::CREATE,
        DATE => lexer::Token::DATE,
        DEFAULT => lexer::Token::DEFAULT,
        DELETE => lexer::Token::DELETE,
        DOUBLE => lexer::Token::DOUBLE,
        ENUM => lexer::Token::ENUM,
        EXTENSION => lexer::Token::EXTENSION,
        FILLFACTOR => lexer::Token::FILLFACTOR,
        FOREIGN => lexer::Token::FOREIGN,
        FULL => lexer::Token::FULL,
        FUNCTION => lexer::Token::FUNCTION,
        INT => lexer::Token::INT,
        INT2 => lexer::Token::INT2,
        INT4 => lexer::Token::INT4,
        INT8 => lexer::Token::INT8,
        INTEGER => lexer::Token::INTEGER,
        INTERNAL => lexer::Token::INTERNAL,
        KEY => lexer::Token::KEY,
        LANGUAGE => lexer::Token::LANGUAGE,
        MATCH => lexer::Token::MATCH,
        MONEY => lexer::Token::MONEY,
        NO => lexer::Token::NO,
        NOT => lexer::Token::NOT,
        NULL => lexer::Token::NULL,
        NUMERIC => lexer::Token::NUMERIC,
        ON => lexer::Token::ON,
        OR => lexer::Token::OR,
        PARTIAL => lexer::Token::PARTIAL,
        PLPGSQL => lexer::Token::PLPGSQL,
        PRECISION => lexer::Token::PRECISION,
        PRIMARY => lexer::Token::PRIMARY,
        REAL => lexer::Token::REAL,
        REFERENCES => lexer::Token::REFERENCES,
        REPLACE => lexer::Token::REPLACE,
        RESTRICT => lexer::Token::RESTRICT,
        RETURNS => lexer::Token::RETURNS,
        SCHEMA => lexer::Token::SCHEMA,
        SERIAL => lexer::Token::SERIAL,
        SERIAL2 => lexer::Token::SERIAL2,
        SERIAL4 => lexer::Token::SERIAL4,
        SERIAL8 => lexer::Token::SERIAL8,
        SET => lexer::Token::SET,
        SIMPLE => lexer::Token::SIMPLE,
        SMALLINT => lexer::Token::SMALLINT,
        SMALLSERIAL => lexer::Token::SMALLSERIAL,
        SQL => lexer::Token::SQL,
        TABLE => lexer::Token::TABLE,
        TEXT => lexer::Token::TEXT,
        TIME => lexer::Token::TIME,
        TIMESTAMP => lexer::Token::TIMESTAMP,
        TIMESTAMPTZ => lexer::Token::TIMESTAMPTZ,
        TIMETZ => lexer::Token::TIMETZ,
        TYPE => lexer::Token::TYPE,
        UNIQUE => lexer::Token::UNIQUE,
        UPDATE => lexer::Token::UPDATE,
        UUID => lexer::Token::UUID,
        VARBIT => lexer::Token::VARBIT,
        VARCHAR => lexer::Token::VARCHAR,
        VARYING => lexer::Token::VARYING,
        WITH => lexer::Token::WITH,
        WITHOUT => lexer::Token::WITHOUT,
        ZONE => lexer::Token::ZONE,

        Ident => lexer::Token::Identifier(<String>),
        Digit => lexer::Token::Digit(<i32>),
        Boolean => lexer::Token::Boolean(<bool>),
        String => lexer::Token::StringValue(<String>),
        Literal => lexer::Token::Literal(<String>),
    }
}

pub statement_list: Vec<Statement> = {
    <v:statement_list> <stmt:statement> => {
        let mut v = v;
        v.push(stmt);
        v
    },
    <statement> => vec!(<>),
};

statement: Statement = {
    CREATE EXTENSION <name:Ident> ";"? => Statement::Extension(ExtensionDefinition {
        name: name,
    }),
    CREATE (OR REPLACE)? FUNCTION <name:object_name> "(" ")" RETURNS <return_type:function_return_type> AS <body:Literal> LANGUAGE <lang:function_type> ";"? => Statement::Function(FunctionDefinition {
        name: name,
        arguments: Vec::new(),
        return_type: return_type,
        body: body,
        language: lang,
    }),
    CREATE (OR REPLACE)? FUNCTION <name:object_name> "(" <args:function_argument_list> ")" RETURNS <return_type:function_return_type> AS <body:Literal> LANGUAGE <lang:function_type> ";"? => Statement::Function(FunctionDefinition {
        name: name,
        arguments: args,
        return_type: return_type,
        body: body,
        language: lang,
    }),
    CREATE SCHEMA <name:Ident> ";"? => Statement::Schema(SchemaDefinition {
        name: name,
    }),
    CREATE TABLE <name:object_name> "(" <columns:column_definition_list> "," <table_constraints:table_constraint_list> ")" ";"? => Statement::Table(TableDefinition {
        name: name,
        columns: columns,
        constraints: Some(table_constraints),
    }),
    CREATE TABLE <name:object_name> "(" <columns:column_definition_list> ")" ";"? => Statement::Table(TableDefinition {
        name: name,
        columns: columns,
        constraints: None,
    }),
    CREATE TYPE <name:Ident> AS ENUM "(" <values:enum_value_list> ")" ";"? => Statement::Type(TypeDefinition {
        name: name,
        kind: TypeDefinitionKind::Enum(values),
    }),
};

object_name: ObjectName = {
    <schema:Ident> "." <name:Ident> => ObjectName { schema: Some(schema), name: name },
    <name:Ident> => ObjectName { schema: None, name: name },
};

column_definition_list: Vec<ColumnDefinition> = {
    <v:column_definition_list> "," <c:column_definition> => {
        let mut v = v;
        v.push(c);
        v
    },
    <column_definition> => vec!(<>)
};

column_definition: ColumnDefinition = {
    <name:Ident> <t:sql_type> <constraints:column_constraint_list?> => ColumnDefinition {
        name: name,
        sql_type: t,
        constraints: constraints,
    },
};

column_list: Vec<String> = {
    <v:column_list> "," <c:Ident> => {
        let mut v = v;
        v.push(c);
        v
    },
    <Ident> => vec!(<>),
};

pub function_argument_list: Vec<FunctionArgument> = {
    <v:function_argument_list> "," <a:function_argument> => {
        let mut v = v;
        v.push(a);
        v
    },
    <function_argument> => vec!(<>)
};

function_argument: FunctionArgument = {
    <name:Ident> <t:sql_type> => FunctionArgument {
        name: name,
        sql_type: t,
    }
};

pub function_return_type: FunctionReturnType = {
    TABLE "(" <columns:column_definition_list> ")" => FunctionReturnType::Table(columns),
    <t:sql_type> => FunctionReturnType::SqlType(t),
};

function_type: FunctionLanguage = {
    C => FunctionLanguage::C,
    INTERNAL => FunctionLanguage::Internal,
    PLPGSQL => FunctionLanguage::PostgreSQL,
    SQL => FunctionLanguage::SQL,
};

table_constraint_list: Vec<TableConstraint> = {
    <v:table_constraint_list> "," <c:table_constraint> => {
        let mut v = v;
        v.push(c);
        v
    },
    <table_constraint> => vec!(<>),
};

table_constraint: TableConstraint = {
    CONSTRAINT <name:Ident> PRIMARY KEY "(" <columns:column_list> ")" <parameters:index_parameters?> => TableConstraint::Primary {
        name: name,
        columns: columns,
        parameters: parameters
    },
    CONSTRAINT <name:Ident> FOREIGN KEY "(" <columns:column_list> ")" REFERENCES <ref_table:object_name> "(" <ref_columns:column_list> ")" <match_type:match_type?> <events:constraint_events?> => TableConstraint::Foreign {
        name: name,
        columns: columns,
        ref_table: ref_table,
        ref_columns: ref_columns,
        match_type: match_type,
        events: events,
    },
};

index_parameters: Vec<IndexParameter> = {
    WITH "(" <index_parameter_list> ")" => <>,
};

index_parameter_list: Vec<IndexParameter> = {
    <v:index_parameter_list> "," <i:index_parameter> => {
        let mut v = v;
        v.push(i);
        v
    },
    <index_parameter> => vec!(<>),
};

index_parameter: IndexParameter = {
    FILLFACTOR "=" <Digit> => IndexParameter::FillFactor(<> as u32),
};

match_type: ForeignConstraintMatchType = {
    MATCH SIMPLE => ForeignConstraintMatchType::Simple,
    MATCH PARTIAL => ForeignConstraintMatchType::Partial,
    MATCH FULL => ForeignConstraintMatchType::Full,
};

constraint_events: Vec<ForeignConstraintEvent> = {
    <v:constraint_events> <e:constraint_event> => {
        let mut v = v;
        v.push(e);
        v
    },
    <constraint_event> => vec!(<>),
};

constraint_event: ForeignConstraintEvent = {
    ON DELETE <foreign_constraint_action> => ForeignConstraintEvent::Delete(<>),
    ON UPDATE <foreign_constraint_action> => ForeignConstraintEvent::Update(<>),
};

foreign_constraint_action: ForeignConstraintAction = {
    NO ACTION => ForeignConstraintAction::NoAction,
    RESTRICT => ForeignConstraintAction::Restrict,
    CASCADE => ForeignConstraintAction::Cascade,
    SET NULL => ForeignConstraintAction::SetNull,
    SET DEFAULT => ForeignConstraintAction::SetDefault,
};

sql_type: SqlType = {
    <simple_type> => SqlType::Simple(<>),
    <simple:simple_type> <dim:array_dimension> => SqlType::Array(simple, dim),
    <Ident> => SqlType::Custom(<>, None),
    <name:Ident> "(" <options:Ident> ")" => SqlType::Custom(name, Some(options)),
};

simple_type: SimpleSqlType = {
    CHAR "(" <Digit> ")" => SimpleSqlType::FixedLengthString(<> as u32),
    CHARACTER "(" <Digit> ")" => SimpleSqlType::FixedLengthString(<> as u32),
    VARCHAR "(" <Digit> ")" => SimpleSqlType::VariableLengthString(<> as u32),
    CHARACTER VARYING "(" <Digit> ")" => SimpleSqlType::VariableLengthString(<> as u32),
    CHARACTER VARYING => SimpleSqlType::UnsizedVariableLengthString,
    TEXT => SimpleSqlType::Text,

    BIT "(" <Digit> ")" => SimpleSqlType::FixedLengthBitString(<> as u32),
    VARBIT "(" <Digit> ")" => SimpleSqlType::VariableLengthBitString(<> as u32),
    BIT VARYING "(" <Digit> ")" => SimpleSqlType::VariableLengthBitString(<> as u32),
    SMALLINT => SimpleSqlType::SmallInteger,
    INT => SimpleSqlType::Integer,
    INTEGER => SimpleSqlType::Integer,
    BIGINT => SimpleSqlType::BigInteger,
    INT2 => SimpleSqlType::SmallInteger,
    INT4 => SimpleSqlType::Integer,
    INT8 => SimpleSqlType::BigInteger,

    SMALLSERIAL => SimpleSqlType::SmallSerial,
    SERIAL => SimpleSqlType::Serial,
    BIGSERIAL => SimpleSqlType::BigSerial,
    SERIAL2 => SimpleSqlType::SmallSerial,
    SERIAL4 => SimpleSqlType::Serial,
    SERIAL8 => SimpleSqlType::BigSerial,

    NUMERIC "(" <m:Digit> "," <d:Digit> ")" => SimpleSqlType::Numeric(m as u32, d as u32),
    DOUBLE PRECISION => SimpleSqlType::Double,
    REAL => SimpleSqlType::Single,
    MONEY => SimpleSqlType::Money,
    BOOL => SimpleSqlType::Boolean,
    BOOLEAN => SimpleSqlType::Boolean,

    DATE => SimpleSqlType::Date,
    TIMESTAMP => SimpleSqlType::DateTime,
    TIMESTAMP WITHOUT TIME ZONE => SimpleSqlType::DateTime,
    TIMESTAMP WITH TIME ZONE => SimpleSqlType::DateTimeWithTimeZone,
    TIMESTAMPTZ => SimpleSqlType::DateTimeWithTimeZone,
    TIME => SimpleSqlType::Time,
    TIME WITHOUT TIME ZONE => SimpleSqlType::Time,
    TIME WITH TIME ZONE => SimpleSqlType::TimeWithTimeZone,
    TIMETZ => SimpleSqlType::TimeWithTimeZone,

    UUID => SimpleSqlType::Uuid,
};

array_dimension: u32 = {
    "[" "]" => 1u32,
    <dim:array_dimension> "[" "]" => dim + 1u32,
};

column_constraint_list: Vec<ColumnConstraint> = {
    <v:column_constraint_list> <q:column_constraint> => {
        let mut v = v;
        v.push(q);
        v
    },
    <column_constraint> => vec!(<>),
};

column_constraint: ColumnConstraint = {
    DEFAULT <any_value> => ColumnConstraint::Default(<>),
    DEFAULT "(" <any_value> ")" => ColumnConstraint::Default(<>),
    NULL => ColumnConstraint::Null,
    NOT NULL => ColumnConstraint::NotNull,
    UNIQUE => ColumnConstraint::Unique,
    PRIMARY KEY => ColumnConstraint::PrimaryKey,
};

any_value: AnyValue = {
    <Boolean> => AnyValue::Boolean(<>),
    <Digit> => AnyValue::Integer(<>),
    <String> => AnyValue::String(<>),
};

enum_value_list: Vec<String> = {
    <v:enum_value_list> "," <e:String> => {
        let mut v = v;
        v.push(e);
        v
    },
    <String> => vec!(<>),
};
